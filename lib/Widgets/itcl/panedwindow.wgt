##############################################################################
#
# Visual TCL - A cross-platform application development environment
#
# Copyright (C) 2001 Christian Gavin
#
# Description file for [Incr Widgets]
#
# This program is free software; you can redistribute it and/or
# modify it under the terms of the GNU General Public License
# as published by the Free Software Foundation; either version 2
# of the License, or (at your option) any later version.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program; if not, write to the Free Software
# Foundation, Inc., 675 Mass Ave, Cambridge, MA 02139, USA.

##############################################################################
#

Class		Panedwindow
Lib		itcl

CreateCmd	::iwidgets::panedwindow
Icon		icon_panedwindow.gif
DumpChildren	no
MegaWidget	yes
TreeChildrenCmd	{
		vTcl:widget:panedwindow:treeChildrenCmd
		vTcl:widget:panedwindow:treeChildrenChildsite
		}
DumpCmd		vTcl:dump:panedwindow
InsertCmd	vTcl:widget:panedwindow:inscmd
DeleteSiteCmd   vTcl:widget:panedwindow:deleteSiteCmd
TagsCmd         vTcl:widget:panedwindow:tagscmd
CompoundCmd     vTcl:compound:panedwindow

AdditionalClasses Pane

## Pane specific options
NewOption -minimum		"min size"		type

## Pane editing support
NewOption       -_itcl_panedwindow_panes  "panes" combobox
ClassOption     -_itcl_panedwindow_panes
OptionConfigCmd -_itcl_panedwindow_panes    config     vTcl::widgets::iwidgets::panedwindow::configPanes
OptionConfigCmd -_itcl_panedwindow_panes    update     vTcl::widgets::iwidgets::panedwindow::updatePanes
OptionConfigCmd -_itcl_panedwindow_panes    get        vTcl::widgets::iwidgets::panedwindow::getPanes
OptionConfigCmd -_itcl_panedwindow_panes    select     vTcl::widgets::iwidgets::panedwindow::selectPane
OptionConfigCmd -_itcl_panedwindow_panes    edit       vTcl::itemEdit::edit
OptionConfigCmd -_itcl_panedwindow_panes    editArg    vTcl::widgets::iwidgets::panedwindow::edit

###########################################################
## Code for showing panes in attributes editor
##
namespace eval vTcl::widgets::iwidgets::panedwindow {

    proc getPanes {target} {
    }

    proc updatePanes {target var} {
        global vTcl

        ## there is a trace on var to update the combobox
        ## first item in the list is the current index
        set sites [$target childsite]
        set num_pages [llength $sites]
        set values 0
        for {set i 1} {$i <= $num_pages} {incr i} {
            lappend values "Pane $i"
        }

        ## this will trigger the trace
        set ::$var $values
    }

    proc configPanes {target var} {
    }

    proc selectPane {target index} {
    }
}

###########################################################
## Code for editing pages in paned window widgets
##
namespace eval vTcl::widgets::iwidgets::panedwindow::edit {

    proc getTitle {target} {
        return "Edit panes for $target"
    }

    proc getLabelOption {} {
        return -label
    }

    proc getItems {target} {
        ## first item in the list is the current index
        set sites [$target childsite]
        set current 0
        set values $current
        for {set i 1} {$i <= [llength $sites]} {incr i} {
            lappend values "Pane $i"
        }
        return $values
    }

    proc addItem {target} {
        $target add pane_new
        vTcl:init_wtree
        return "New Pane"
    }

    proc removeItem {target index} {
        $target delete $index
        vTcl:init_wtree
    }

    proc itemConfigure {target index args} {
        if {$args == ""} {
            set options [$target paneconfigure $index]
            set result ""
            ## grrr: why do they return options from the widget here with
            ##       three items like {-width 15 10} ?? bug!!
            foreach option $options {
                ## only return valid options
                if {[llength $option] == 5} {
                    lappend result $option
                }
            }
            return $result
        } else {
            eval $target paneconfigure $index $args
        }
    }

    proc moveUpOrDown {target index direction} {
        error "Not implemented yet!"
    }
}

proc vTcl:widget:panedwindow:inscmd {target} {
    $target add pane1
    $target add pane2
}

proc vTcl:widget:panedwindow:tagscmd {target} {

    global vTcl

    # workaround for special binding tags in IWidgets
    set tags $vTcl(bindtags,$target)
    set special1 [lsearch -glob $tags pw-config-*]
    set special2 [lsearch -glob $tags itk-delete-*]

    set class [winfo class $target]
    set toplevel [winfo toplevel $target]
    set result ""

    if {$special1 != -1} {
        lappend result [lindex $tags $special1]
    }
    if {$special2 != -1} {
        lappend result [lindex $tags $special2]
    }

    lappend result $target $class $toplevel all
    return $result
}

# All right, I explain it: -1 is the number of levels
# in the window path between the megawidget and its
# pages (childsites). This is used by the widget tree
# to skip 4 levels (otherwise it would draw 4 vertical
# lines)

proc vTcl:widget:panedwindow:treeChildrenCmd {target {diff \#-1}} {

    set children ""
    set wantsdiff [expr {$diff != ""}]

    foreach site [$target childsite] {
        foreach child [vTcl:complete_widget_tree $site $wantsdiff] {
            lappend children $child$diff
        }
    }

    return $children
}

proc vTcl:widget:panedwindow:treeChildrenChildsite {target} {

    return [$target childsite]
}

proc vTcl:widget:panedwindow:deleteSiteCmd {target site} {

    set index [lsearch -exact [$target childsite] $site]

    if {$index != -1} {
        $target delete $index
    }
}

proc vTcl:dump:panedwindow {target basename} {

    global vTcl
    set result [vTcl:dump_widget_opt $target $basename]
    set entries [$target childsite]
    set size [llength $entries]
    for {set i 0} {$i < $size} {incr i} {
        set conf [$target paneconfigure $i]
        set pairs [vTcl:conf_to_pairs $conf ""]
        append result "$vTcl(tab)$basename add pane$i\n"
        append result "$vTcl(tab)$basename paneconfigure pane$i \\\n"
        append result "[vTcl:clean_pairs $pairs]\n"
    }
    for {set i 0} {$i < $size} {incr i} {
        set page [lindex $entries $i]
        append result "$vTcl(tab)"
        set sitevariable "site_[llength [split $page .]]_$i"
        append result "set $sitevariable "
        append result "\[lindex \[$basename childsite\] $i\]\n"
        append result [vTcl:lib_itcl:dump_subwidgets $page \$$sitevariable]
    }
    return $result
}

## this function returns the necessary code to insert all
## the childsites into the panedwindow widget (basically
## the returned code will be stored in a compound)
##
## the returned code makes the body of a procedure whose
## parameter is 'target'; to insert the childsites into the
## compound, this procedure should be called with target
## pointing to the new widget

proc vTcl:compound:panedwindow {target} {
    set result ""

    set entries [$target childsite]
    set size [llength $entries]
    for {set i 0} {$i < $size} {incr i} {
        set conf [$target paneconfigure $i]
        set pairs [vTcl:conf_to_pairs $conf ""]
        append result "set site($i) \[\$target add pane$i $pairs\]; "
    }

    ## we have to ask for the childsites list, because the add
    ## command doesn't seem to be returning the correct path to the
    ## pane it just added

    for {set i 0} {$i < $size} {incr i} {
        set compound [vTcl:create_compound [lindex $entries $i]]
        append result "vTcl:insert_compound "
        append result "\[lindex \[\$target childsite\] $i\] [list $compound] pack \{\}; "
    }

    return $result
}
